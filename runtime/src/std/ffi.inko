# Foreign function interface for interfacing with C code.
#
# This module provides various types and methods for calling C code from Inko
# code.
#
# # Supported features
#
# * Calling C functions with a fixed number of arguments.
# * Reading from and writing to pointers to C data (e.g. `errno` from libc).
# * Converting various types from Inko to C and back.
#
# # Limitations
#
# * The use of callback functions (= passing an Inko block as a C callback)
#   is currently not supported.
# * Only a limited number of Inko objects can be converted to C values.
#   Converting custom objects is not supported.
# * The garbage collector does not automatically free any resources allocated
#   in C, instead this must be done manually (e.g. using `free` from libc).
# * Variadic arguments are not supported at this time.
#
# # Thread-local storage
#
# Inko using pre-emptive scheduling can result in a process being moved between
# OS threads. This means that if a C function reads or writes to thread-local
# storage, any following calls to C functions may execute on a different thread
# and thus not have access to the same thread-local data.
#
# To prevent this from happening, you should wrap FFI code that relies on
# thread-local storage in a `process.pinned` block. For example:
#
#     import std::ffi::Library
#     import std::ffi::types
#     import std::process
#
#     let libc = Library.new(Array.new('libc.so.6'))
#
#     process.pinned {
#       let errno = libc.variable('errno')
#
#       errno.write(types.i32, 1)
#       errno.read(types.i32) # => 1
#     }
#
# # Usage
#
# The first step is two import the symbols we need:
#
#     import std::ffi::Library
#     import std::ffi::types
#
# The `Library` object will be used for loading libraries. The
# `std::ffi::types` module will be used for defining functions. Let's start by
# loading libc:
#
#     import std::ffi::Library
#     import std::ffi::types
#
#     let libc = Library.new(Array.new('libc.so.6'))
#
# `Library.new` takes an `Array!(String)` to use for searching libraries. The
# values can be library file names, relative paths, or absolute paths. If
# multiple values are given, `Library.new` will try to load them in order,
# stopping when it finds a library. This allows us to provide alternative
# paths, should the first one not be available:
#
#     import std::ffi::Library
#     import std::ffi::types
#
#     let libc = Library.new(Array.new('libc.so', 'libc.so.6', 'libSystem.dylib'))
#
# Once the library has been loaded, we can attach functions by sending the
# `function` message to the library:
#
#     import std::ffi::Library
#     import std::ffi::types
#
#     let libc = Library.new(Array.new('libc.so.6'))
#     let puts = libc.function('puts', Array.new(types.string), types.i32)
#
# Here we attach the `puts` function. The arguments we defined are
# `Array.new(types.string)`, with the return type being `types.i32`.
#
# We can call our function by sending `call` to it:
#
#     import std::ffi::Library
#     import std::ffi::types
#
#     let libc = Library.new(Array.new('libc.so.6'))
#     let puts = libc.function('puts', Array.new(types.string), types.i32)
#
#     puts.call('hello')
#
# We can also attach variables by sending `variable` to a `Library`:
#
#     import std::ffi::Library
#     import std::ffi::types
#
#     let libc = Library.new(Array.new('libc.so.6'))
#     let errno = libc.variable('errno')
#
#     errno.read(types.i32) # => 0
import std::clone::(Clone, Copy)
import std::conversion::ToInt
import std::error::StandardError
import std::hash::Hash
import std::index::(Index, SetIndex)
import std::io::Close
import std::map::Map
import std::operators::Equal
import std::process::(panic)
import std::set::Set

# A dynamically loaded C library.
let Library = _INKOC.get_ffi_library_prototype

# A C function that can be called from Inko.
let Function = _INKOC.get_ffi_function_prototype

# A pointer to a value in C.
let Pointer = _INKOC.get_ffi_pointer_prototype

_INKOC.set_object_name(Library, 'Library')
_INKOC.set_object_name(Function, 'Function')
_INKOC.set_object_name(Pointer, 'Pointer')

# The address used for NULL pointers.
let NULL_ADDRESS = 0x0

extern def ffi_library_open(names: ref Array!(String)) !! String -> Library
extern def ffi_function_attach(
  library: ref Library,
  name: ref String,
  arguments: ref Array!(Int),
  returns: ref Int
) !! String -> Function

extern def ffi_function_call(
  function: ref Function,
  arguments: ref Array!(ref Any)
) -> Any

extern def ffi_pointer_attach(
  library: ref Library,
  name: ref String
) !! String -> Pointer

extern def ffi_pointer_read(
  pointer: ref Pointer,
  type: ref Int,
  offset: ref Int
) -> Any

extern def ffi_pointer_write(
  pointer: ref Pointer,
  type: ref Int,
  value: ref Any,
  offset: ref Int
) -> Any

extern def ffi_pointer_from_address(address: ref Int) -> Pointer
extern def ffi_pointer_address(pointer: ref Pointer) -> Int
extern def ffi_type_size(type: ref Int) -> Int
extern def ffi_type_alignment(type: ref Int) -> Int

impl Pointer {
  # Creates a pointer using the given memory address.
  static def new(address: Int) -> Self {
    ffi_pointer_from_address(address)
  }

  # Returns a NULL `Pointer`.
  static def null -> Self {
    new(NULL_ADDRESS)
  }

  # Returns `True` if this `Pointer` is a NULL pointer.
  def null? -> Boolean {
    address == NULL_ADDRESS
  }

  # Returns the address of this `Pointer`.
  def address -> Int {
    ffi_pointer_address(self)
  }

  # Reads the value of a pointer into a certain type.
  #
  # The `type` argument specifies the `Type` of data that is being read.
  #
  # The `offset` argument is used to specify an offset in bytes (relative to the
  # pointer) to read from.
  def read(type: ref Type, offset: Int) -> Any {
    ffi_pointer_read(self, type.to_int, offset)
  }

  # Writes a value to the pointer.
  #
  # The `type` argument specifies the `Type` of data that is being written. The
  # `offset` argument can be used to specify an offset in bytes (relative to
  # the pointer) to write to.
  def write(type: ref Type, value: Any, offset: Int) {
    ffi_pointer_write(self, type.to_int, value, offset)
  }
}

impl Object for Pointer {}

impl Equal for Pointer {
  # Returns `True` if `self` and the given `Pointer` point to the same memory
  # address.
  def ==(other: Self) -> Boolean {
    address == other.address
  }
}

impl Hash for Pointer {}

impl Library {
  # Dynamically loads a C library.
  #
  # The `names` argument is an `Array` of library names to use for loading the
  # library. These names are used in order to find the corresponding library.
  #
  # The names in this `Array` can either be the library name with extension,
  # such as `'libc.so.6'`, or absolute paths such as `'/usr/lib/libc.so.6'`.
  #
  # This method throws if the library can't be loaded.
  static def new(names: Array!(String)) !! StandardError -> Self {
    try ffi_library_open(names) else (error) throw StandardError.new(error)
  }

  # Returns a `Pointer` pointing to the address of the given variable.
  #
  # This method throws if there is no symbol with the given name.
  def variable(name: String) !! StandardError -> Pointer {
    try ffi_pointer_attach(self, name) else (e) throw StandardError.new(e)
  }
}

impl Object for Library {}

impl Close for Library {
  # Disposes of the current library.
  move def close {
    _INKOC.close(self)
  }
}

impl Function {
  # Loads a C function from the given library.
  #
  # This method throws if there is no function with the given name.
  #
  # # Panics
  #
  # This method will panic if the argument types or return type are invalid
  # values.
  static def new(
    library: ref Library,
    name: String,
    arguments: Array!(Type),
    returns: Type
  ) !! StandardError -> Self {
    try {
      ffi_function_attach(
        library,
        name,
        arguments.iter.map do (type) { type.to_int }.to_array,
        returns.to_int
      )
    } else (error) {
      throw StandardError.new(error)
    }
  }

  # Calls the function with the given arguments.
  #
  # This method will panic if any of the arguments are invalid.
  def call(*arguments: ref Any) -> Any {
    ffi_function_call(self, arguments)
  }
}

impl Object for Function {}

# Now that `Function` has been defined, we can define `Library.function` to make
# it a bit easier to attach functions.
impl Library {
  # Loads a C function from the given library.
  #
  # See `Function.new` for more information.
  def function(
    name: String,
    arguments: Array!(Type),
    returns: Type
  ) !! StandardError -> Function {
    try Function
      .new(library: self, name: name, arguments: arguments, returns: returns)
  }
}

# A member in a structure.
class Member {
  # The name of this `Member`.
  @name: String

  # The C `Type` of this `Member`.
  @type: Type

  # The offset of this `Member`, relative to the start of the struct.
  @offset: Int

  static def new(name: String, type: Type, offset: Int) -> Self {
    Self { @name = name, @type = type, @offset = offset }
  }

  # Returns the name of this `Member`.
  def name -> String {
    @name.clone
  }

  # Returns the C `Type` of this `Member`.
  def type -> Type {
    @type.clone
  }

  # Returns the offset of this `Member`.
  def offset -> Int {
    @offset.clone
  }
}

# The members, alignment, and other information used for building a C
# structure.
class Layout {
  # The members of this layout, mapped to their names.
  @members: Map!(String, Member)

  # The size (in bytes) of this layout.
  @size: Int

  # The alignment to use for all fields. This equals the alignment of the
  # largest field.
  @alignment: Int

  static def new(
    members: Map!(String, Member),
    alignment: Int,
    size: Int
  ) -> Self {
    Self { @members = members, @size = size, @alignment = alignment }
  }

  # Returns the alignment of the members in this `Struct`.
  def alignment -> Int {
    @alignment.clone
  }

  # Returns the size in bytes of this `Layout`.
  def size -> Int {
    @size.clone
  }
}

impl Index!(String, ref Member) for Layout {
  # Returns the `Member` for the given name.
  #
  # This method will panic if no member is defined for the given name.
  def [](index: String) -> ref Member {
    @members[index]
  }
}

# A C structure.
class Struct {
  # The pointer to the C address that contains the structure.
  @pointer: Pointer

  # The `Layout` of this structure.
  @layout: Layout

  static def new(pointer: Pointer, layout: Layout) -> Self {
    Self { @pointer = pointer, @layout = layout }
  }

  # Returns the size in bytes of this `Struct`.
  def size -> Int {
    @layout.size
  }

  # Returns the alignment of the members in this `Struct`.
  def alignment -> Int {
    @layout.alignment
  }

  # Returns the pointer to the C structure.
  def pointer -> ref Pointer {
    @pointer
  }
}

impl Index!(String, Any) for Struct {
  # Reads the value of the given struct member.
  #
  # This method will panic if the given member does not exist.
  def [](index: String) -> Any {
    let member = @layout[index]

    @pointer.read(type: member.type, offset: member.offset)
  }
}

impl SetIndex!(String, Any) for Struct {
  # Writes a value to a struct member.
  def []=(index: String, value: Any) {
    let member = @layout[index]

    @pointer.write(type: member.type, value: value, offset: member.offset)
  }
}

# These methods require that `Struct` is defined first.
impl Layout {
  # Creates a new `Struct` using this layout.
  #
  # The given argument is a `Pointer` pointing to the memory in C that contains
  # the structure's data.
  move def from_pointer(pointer: Pointer) -> Struct {
    Struct.new(pointer: pointer, layout: self)
  }
}

# An object used for constructing an immutable `Layout`.
class LayoutBuilder {
  # The names of all members, in the order they are defined in.
  @members: Array!(String)

  # The types of all members, in the order they are defined in.
  @types: Array!(Type)

  # A Map that tracks which members have already been defined.
  @existing: Set!(String)

  # The alignment of the largest field.
  @alignment: Int

  # A boolean indicating if members should be padded or not.
  @padding: Boolean

  static def new -> Self {
    Self {
      @members = Array.new,
      @types = Array.new,
      @existing = Set.new,
      @alignment = 0,
      @padding = True
    }
  }

  # Disables the padding of members.
  def disable_padding {
    @padding = False
    @alignment = 1
  }

  # Creates a new `Layout` based on the current state of this builder.
  def to_layout -> Layout {
    if @padding { return layout_with_padding }

    layout_without_padding
  }

  # Creates a `Layout` that automatically applies padding.
  def layout_with_padding -> Layout {
    let members = Map.new
    let mut size = 0
    let mut offset = 0
    let mut remaining_in_hole = @alignment

    for (index, name) in @members.iter.with_index {
      let type = @types[index]
      let type_align = type.alignment

      # If the value is too great to fit into the current hole, we need to add
      # padding to the current hole, then start over at the next hole.
      if type_align > remaining_in_hole {
        let padding = @alignment - remaining_in_hole

        size += padding
        offset += padding

        remaining_in_hole = @alignment
      }

      members[name.clone] =
        Member.new(name: name.clone, type: type.clone, offset: offset)

      remaining_in_hole -= type_align

      size += type_align
      offset += type_align

      if remaining_in_hole.zero? { remaining_in_hole = @alignment }
    }

    Layout.new(members: members, alignment: @alignment.clone, size: size)
  }

  # Creates a `Layout` that does not use any padding.
  def layout_without_padding -> Layout {
    let members = Map.new
    let mut offset = 0

    for (index, name) in @members.iter.with_index {
      let type = @types[index]

      members[name.clone] =
        Member.new(name: name.clone, type: type.clone, offset: offset)

      offset += type.alignment
    }

    Layout.new(members: members, alignment: @alignment.clone, size: offset)
  }
}

impl SetIndex!(String, Type) for LayoutBuilder {
  # Defines a new member.
  #
  # Trying to redefine a member will result in a panic.
  def []=(name: String, type: Type) {
    if @existing.contains?(name) {
      panic(`The member {name} has already been defined`)
    }

    @existing.insert(name.clone)
    @members.push(name)
    @types.push(type)

    # If padding is enabled we determine the structure's alignment based on the
    # alignment of the largest field.
    if @padding and { type.alignment > @alignment } {
      @alignment = type.alignment
    }
  }
}

# An identifier for a C type.
#
# A Type is just a simple wrapper around an Int. The use of a `Type`
# instead of a regular `Int` makes it a little more difficult to use
# incorrect type identifiers when attaching C functions.
class Type {
  # The numeric identifier of this type.
  @id: Int

  static def new(id: Int) -> Self {
    Self { @id = id }
  }

  # Returns the size of this type in bytes.
  def size -> Int {
    ffi_type_size(@id)
  }

  # Returns the alignment of this type in bytes.
  def alignment -> Int {
    ffi_type_alignment(@id)
  }
}

impl Equal for Type {
  def ==(other: Type) -> Boolean {
    to_int == other.to_int
  }
}

impl ToInt for Type {
  # Returns the numeric identifier of this type.
  def to_int -> Int {
    @id.clone
  }
}

impl Clone for Type {
  def clone -> Self {
    Type.new(@id.clone)
  }
}

impl Copy for Type {}

# Returns a `Type` for the C type `void`.
def void -> Type {
  Type.new(0)
}

# Returns a `Type` for the C type `void *`.
def pointer -> Type {
  Type.new(1)
}

# Returns a `Type` for the C type `double`.
def f64 -> Type {
  Type.new(2)
}

# Returns a `Type` for the C type `float`.
def f32 -> Type {
  Type.new(3)
}

# Returns a `Type` for the C type `char`.
def i8 -> Type {
  Type.new(4)
}

# Returns a `Type` for the C type `short`.
def i16 -> Type {
  Type.new(5)
}

# Returns a `Type` for the C type `int`.
def i32 -> Type {
  Type.new(6)
}

# Returns a `Type` for the C type `long`.
def i64 -> Type {
  Type.new(7)
}

# Returns a `Type` for the C type `unsigned char`.
def u8 -> Type {
  Type.new(8)
}

# Returns a `Type` for the C type `unsigned short`.
def u16 -> Type {
  Type.new(9)
}

# Returns a `Type` for the C type `unsigned int`.
def u32 -> Type {
  Type.new(10)
}

# Returns a `Type` for the C type `unsigned long`.
def u64 -> Type {
  Type.new(11)
}

# Returns a `Type` for the C type `size_t`.
def size_t -> Type {
  Type.new(14)
}

# Returns a `Type` for the C type `char *`.
def string -> Type {
  Type.new(12)
}

# Returns a `Type` for the C type `char *`, when you need a `ByteArray` instead
# of a `String`.
def byte_array -> Type {
  Type.new(13)
}

# Builds a new structure `Layout`.
#
# This method can be used to build the layout of a structure, without having to
# explicitly import any additional types.
#
# Despite its name, this method returns a `Layout` and not a `Struct`. The name
# "struct" was chosen because it leads to a more pleasant AP, compared to
# calling the method `layout`.
def struct(block: do (ref LayoutBuilder)) -> Layout {
  let builder = LayoutBuilder.new

  block.call(builder)

  builder.to_layout
}
