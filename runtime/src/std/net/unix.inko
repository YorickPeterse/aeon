# Networking types for Unix domain socket communication.
import std::conversion::ToString
import std::fs::path::Path
import std::io::(Close, Error, Read, Write)
import std::net::bits::(
  self, MAXIMUM_LISTEN_BACKLOG, SOCK_DGRAM, SOCK_RAW, SOCK_SEQPACKET,
  SOCK_STREAM, Socket as RawSocket
)
import std::operators::Equal
import std::option::Option

# A low-level, non-blocking Unix domain socket.
#
# Low-level Unix domain sockets allow for more fine-grained control over how
# sockets should be constructed and used, at the cost of a slightly less
# ergonomic API compared to more high-level types such as `UnixDatagram`.
let Socket = _INKOC.get_unix_socket_prototype

_INKOC.set_object_name(Socket, 'Socket')

impl Object for Socket {}

# The socket type for Unix socket streams.
let STREAM = SOCK_STREAM

# The socket type for Unix datagram sockets.
let DGRAM = SOCK_DGRAM

# The socket type for Unix sequential packet sockets.
let SEQPACKET = SOCK_SEQPACKET

# The socket type for Unix raw sockets.
let RAW = SOCK_RAW

extern def socket_allocate_unix(type: ref Int) !! Int -> Socket
extern def socket_accept_unix(socket: ref Socket) !! Int -> Socket

# A Unix domain socket address.
class SocketAddress {
  # The path or name of the address.
  #
  # This is a `String` since using a `Path` does not make sense for abstract
  # and unnamed addresses.
  @address: String

  # Creates a new `SocketAddress` from the given path or name.
  #
  # # Examples
  #
  # Creating a `SocketAddress` that uses a path:
  #
  #     import std::net::unix::SocketAddress
  #
  #     SocketAddress.new('/tmp/test.sock')
  #
  # Creating a `SocketAddress` that uses an unnamed address:
  #
  #     import std::net::unix::SocketAddress
  #
  #     SocketAddress.new('')
  #
  # Creating a `SocketAddress` that uses an abstract address:
  #
  #     import std::net::unix::SocketAddress
  #
  #     SocketAddress.new("\0example")
  static def new(address: String) -> Self {
    Self { @address = address }
  }

  # Returns the path of this address.
  #
  # If the address is unnamed or an abstract address, None is returned.
  def to_path -> ?Path {
    if unnamed? or abstract? { Option.none }

    Option.some(@address.to_path)
  }

  # Returns `True` if `self` is an abstract address.
  #
  # # Examples
  #
  # Checking if an address is abstract:
  #
  #     import std::net::unix::SocketAddress
  #
  #     SocketAddress.new('/tmp/test.sock').abstract?    # => False
  #     SocketAddress.new("\0example-address").abstract? # => True
  def abstract? -> Boolean {
    @address.starts_with?("\0")
  }

  # Returns `True` if `self` is an unnamed address.
  #
  # # Examples
  #
  # Checking if an address is unnamed:
  #
  #     import std::net::unix::SocketAddress
  #
  #     SocketAddress.new('/tmp/test.sock').unnamed? # => False
  #     SocketAddress.new('').unnamed?               # => True
  def unnamed? -> Boolean {
    @address.empty?
  }
}

impl Equal for SocketAddress {
  # Returns `True` if `self` and `other` are the same socket addresses.
  #
  # # Examples
  #
  # Comparing two `SocketAddress` objects:
  #
  #     import std::net::unix::SocketAddress
  #
  #     SocketAddress.new('a.sock') == SocketAddress.new('a.sock') # => True
  #     SocketAddress.new('a.sock') == SocketAddress.new('b.sock') # => False
  def ==(other: ref Self) -> Boolean {
    @address == other.to_string
  }
}

impl ToString for SocketAddress {
  # Returns the address name or path as a `String`.
  #
  # # Examples
  #
  # Converting a `SocketAddress` to a `String`:
  #
  #     import std::net::unix::SocketAddress
  #
  #     SocketAddress.new('/tmp/test.sock').to_string # => '/tmp/test.sock'
  #     SocketAddress.new("\0example").to_string      # => "\0example"
  def to_string -> String {
    @address.clone
  }
}

impl Socket {
  # Creates a new Unix domain socket.
  #
  # # Examples
  #
  # Creating a new socket:
  #
  #     import std::net::unix::(DGRAM, Socket)
  #
  #     try! Socket.new(DGRAM)
  static def new(kind: Int) !! Error -> Socket {
    try socket_allocate_unix(kind) else (error) throw Error.new(error)
  }

  # Binds this socket to the specified path or abstract address.
  #
  # # Examples
  #
  # Binding a Unix socket to a path:
  #
  #     import std::net::unix::(DGRAM, Socket)
  #
  #     let socket = try! Socket.new(DGRAM)
  #
  #     try! socket.bind('/tmp/test.sock')
  def bind(path: ref ToString) !! Error {
    try bits.bind(self, path.to_string, 0)
  }

  # Connects this socket to the specified address.
  #
  # # Examples
  #
  # Connecting a Unix socket:
  #
  #     import std::net::unix::(STREAM, Socket)
  #
  #     let listener = try! Socket.new(STREAM)
  #     let stream = try! Socket.new(STREAM)
  #
  #     try! listener.bind('/tmp/test.sock')
  #     try! listener.listen
  #
  #     try! stream.connect('/tmp/test.sock')
  def connect(path: ref ToString) !! Error {
    try bits.connect(self, path.to_string, 0)
  }

  # Marks this socket as being ready to accept incoming connections using
  # `accept()`.
  #
  # # Examples
  #
  # Marking a socket as a listener:
  #
  #     import std::net::unix::(Socket, STREAM)
  #
  #     let socket = try! Socket.new(STREAM)
  #
  #     try! socket.bind('/tmp/test.sock')
  #     try! socket.listen
  def listen !! Error -> Int {
    try bits.listen(self, MAXIMUM_LISTEN_BACKLOG)
  }

  # Accepts a new incoming connection from this socket.
  #
  # This method will not return until a connection is available.
  #
  # # Examples
  #
  # Accepting a connection and reading data from the connection:
  #
  #     import std::net::unix::(Socket, STREAM)
  #
  #     let listener = try! Socket.new(STREAM)
  #     let stream = try! Socket.new(STREAM)
  #
  #     try! listener.bind('/tmp/test.sock')
  #     try! listener.listen
  #
  #     try! stream.connect('/tmp/test.sock')
  #     try! stream.write_string('ping')
  #
  #     let client = try! listener.accept
  #
  #     try! client.read_string(4) # => 'ping'
  def accept !! Error -> Socket {
    try socket_accept_unix(self) else (error) throw Error.new(error)
  }

  # Sends a `String` to the given address.
  #
  # The return value is the number of bytes sent.
  #
  # # Examples
  #
  #     import std::net::unix::(Socket, DGRAM)
  #
  #     let socket = try! Socket.new(DGRAM)
  #
  #     try! socket.bind('/tmp/test.sock')
  #     try! socket.send_string_to(string: 'hello', address: '/tmp/test.sock')
  def send_string_to(
    string: ref ToString,
    address: ref ToString
  ) !! Error -> Int {
    try bits.send_string_to(self, string.to_string, address.to_string, 0)
  }

  # Sends a `ByteArray` to the given address.
  #
  # The return value is the number of bytes sent.
  #
  # # Examples
  #
  #     import std::net::unix::(Socket, DGRAM)
  #
  #     let socket = try! Socket.new(DGRAM)
  #     let bytes = 'hello'.to_byte_array
  #
  #     try! socket.bind('/tmp/test.sock')
  #     try! socket.send_bytes_to(bytes: bytes, address: '/tmp/test.sock')
  def send_bytes_to(
    bytes: ref ByteArray,
    address: ref ToString
  ) !! Error -> Int {
    try bits.send_bytes_to(self, bytes, address.to_string, 0)
  }

  # Receives a single datagram message on the socket, returning the address the
  # message was sent from.
  #
  # The message is read into the given `ByteArray`, and up to `size` bytes will
  # be read.
  #
  # # Examples
  #
  # Sending a message to ourselves and receiving it:
  #
  #     import std::net::unix::(Socket, DGRAM)
  #
  #     let socket = try! Socket.new(DGRAM)
  #     let bytes = ByteArray.new
  #
  #     try! socket.send_to(message: 'hello', address: '/tmp/test.sock')
  #
  #     let received_from = try! socket.receive_from(bytes: bytes, size: 5)
  #
  #     bytes.to_string         # => 'hello'
  #     received_from.to_string # => '/tmp/test.sock'
  def receive_from(
    bytes: ref ByteArray,
    size: Int
  ) !! Error -> SocketAddress {
    let (addr: String) =
      try bits.receive_from(socket: self, bytes: bytes, size: size)

    SocketAddress.new(addr)
  }

  # Returns the local address of this socket.
  def local_address !! Error -> SocketAddress {
    let (addr: String) = try bits.local_address(self)

    SocketAddress.new(addr)
  }

  # Returns the peer address of this socket.
  def peer_address !! Error -> SocketAddress {
    let (addr: String) = try bits.peer_address(self)

    SocketAddress.new(addr)
  }

  # Returns the value of the `SO_RCVBUF` option.
  def receive_buffer_size !! Error -> Int {
    try bits.get_recv_size(self)
  }

  # Sets the value of the `SO_RCVBUF` option.
  def receive_buffer_size=(value: Int) !! Error -> Int {
    try bits.set_recv_size(self, value)
  }

  # Returns the value of the `SO_SNDBUF` option.
  def send_buffer_size !! Error -> Int {
    try bits.get_send_size(self)
  }

  # Sets the value of the `SO_SNDBUF` option.
  def send_buffer_size=(value: Int) !! Error -> Int {
    try bits.set_send_size(self, value)
  }

  # Shuts down the reading half of this socket.
  def shutdown_read !! Error {
    try bits.shutdown_read(self)
  }

  # Shuts down the writing half of this socket.
  def shutdown_write !! Error {
    try bits.shutdown_write(self)
  }

  # Shuts down both the reading and writing half of this socket.
  def shutdown !! Error {
    try bits.shutdown(self)
  }
}

impl RawSocket for Socket {}

impl Read for Socket {
  def read(into: ref ByteArray, size: Int) !! Error -> Int {
    try bits.read_bytes(self, into, size)
  }
}

impl Write for Socket {
  def write_bytes(bytes: ref ByteArray) !! Error -> Int {
    try bits.write_bytes(self, bytes)
  }

  def write_string(string: ref ToString) !! Error -> Int {
    try bits.write_string(self, string.to_string)
  }

  def flush {
    # Sockets can't be flushed, so this method is just a noop.
  }
}

impl Close for Socket {
  move def close {
    _INKOC.close(self)
  }
}

# A Unix datagram socket.
class UnixDatagram {
  # The raw `Socket` wrapped by this `UnixDatagram`.
  @socket: Socket

  # Creates a new Unix datagram socket bound to the given address.
  #
  # # Examples
  #
  # Creating a new Unix datagram socket:
  #
  #     import std::net::unix::UnixDatagram
  #
  #     try! UnixDatagram.new('/tmp/test.sock')
  static def new(address: ref ToString) !! Error -> Self {
    let socket = try Socket.new(DGRAM)

    try socket.bind(address)

    Self { @socket = socket }
  }

  # Connects `self` to the remote addres.s
  #
  # Connecting a `UnixDatagram` allows sending and receiving data using the
  # methods from `std::io::Read` and `std::io::Write`, instead of having to use
  # `UnixDatagram.receive_from` and `UnixDatagram.send_to`.
  #
  # # Examples
  #
  # Connecting a Unix datagram socket:
  #
  #     import std::net::unix::UnixDatagram
  #
  #     let socket1 = try! UnixDatagram.new('/tmp/test1.sock')
  #     let socket2 = try! UnixDatagram.new('/tmp/test2.sock')
  #
  #     try! socket1.connect('/tmp/test2.sock')
  def connect(address: ref ToString) !! Error {
    try @socket.connect(address.to_string)
  }

  # Sends a `String` to the given address.
  #
  # See the documentation of `Socket.send_string_to` for more information.
  #
  # # Examples
  #
  #     import std::net::unix::UnixDatagram
  #
  #     let socket = try! UnixDatagram.new('/tmp/test.sock')
  #
  #     try! socket.send_string_to(string: 'hello', address: '/tmp/test.sock')
  def send_string_to(
    string: ref ToString,
    address: ref ToString
  ) !! Error -> Int {
    try @socket.send_string_to(string.to_string, address.to_string)
  }

  # Sends a `ByteArray` to the given address.
  #
  # See the documentation of `Socket.send_bytes_to` for more information.
  #
  # # Examples
  #
  #     import std::net::unix::UnixDatagram
  #
  #     let socket = try! UnixDatagram.new('/tmp/test.sock')
  #     let bytes = 'hello'.to_byte_array
  #
  #     try! socket.send_bytes_to(bytes: bytes, address: '/tmp/test.sock')
  def send_bytes_to(
    bytes: ref ByteArray,
    address: ref ToString
  ) !! Error -> Int {
    try @socket.send_bytes_to(bytes, address.to_string)
  }

  # Receives a single datagram message on the socket, returning the address the
  # message was sent from.
  #
  # See the documentation of `Socket.receive_from` for more information.
  def receive_from(
    bytes: ref ByteArray,
    size: Int
  ) !! Error -> SocketAddress {
    try @socket.receive_from(bytes: bytes, size: size)
  }

  # Returns the local address of this socket.
  #
  # See the documentation of `Socket.local_address` for more information.
  def local_address !! Error -> SocketAddress {
    try @socket.local_address
  }

  # Returns the underlying `Socket` object.
  #
  # This method can be used to set additional low-level socket options, without
  # `UnixDatagram` having to re-define all these methods.
  def socket -> ref Socket {
    @socket
  }
}

impl Read for UnixDatagram {
  def read(into: ref ByteArray, size: Int) !! Error -> Int {
    try @socket.read(into, size)
  }
}

impl Write for UnixDatagram {
  def write_bytes(bytes: ref ByteArray) !! Error -> Int {
    try @socket.write_bytes(bytes)
  }

  def write_string(string: ref ToString) !! Error -> Int {
    try @socket.write_string(string.to_string)
  }

  def flush {
    @socket.flush
  }
}

impl Close for UnixDatagram {
  move def close {
    @socket.close
  }
}

# A Unix stream socket connected to another Unix socket.
class UnixStream {
  # The raw `Socket` wrapped by this `UnixStream`.
  @socket: Socket

  # Creates a new `UnixStream` that is connected to the given address.
  #
  # # Examples
  #
  # Connecting a `UnixStream`:
  #
  #     import std::net::unix::(UnixListener, UnixStream)
  #
  #     let listener = try! UnixListener.new('/tmp/test.sock')
  #
  #     try! UnixStream.new('/tmp/test.sock')
  static def new(address: ref ToString) !! Error -> Self {
    let socket = try Socket.new(STREAM)

    try socket.connect(address.to_string)

    Self { @socket = socket }
  }

  # Returns the local address of this socket.
  #
  # See the documentation of `Socket.local_address` for more information.
  def local_address !! Error -> SocketAddress {
    try @socket.local_address
  }

  # Returns the peer address of this socket.
  #
  # See the documentation of `Socket.peer_address` for more information.
  def peer_address !! Error -> SocketAddress {
    try @socket.peer_address
  }

  # Returns the underlying `Socket` object.
  #
  # This method can be used to set additional low-level socket options, without
  # `UnixStream` having to re-define all these methods.
  def socket -> ref Socket {
    @socket
  }

  # Shuts down the reading half of this socket.
  def shutdown_read !! Error {
    try @socket.shutdown_read
  }

  # Shuts down the writing half of this socket.
  def shutdown_write !! Error {
    try @socket.shutdown_write
  }

  # Shuts down both the reading and writing half of this socket.
  def shutdown !! Error {
    try @socket.shutdown
  }
}

impl Read for UnixStream {
  def read(into: ref ByteArray, size: Int) !! Error -> Int {
    try @socket.read(into, size)
  }
}

impl Write for UnixStream {
  def write_bytes(bytes: ref ByteArray) !! Error -> Int {
    try @socket.write_bytes(bytes)
  }

  def write_string(string: ref ToString) !! Error -> Int {
    try @socket.write_string(string.to_string)
  }

  def flush {
    @socket.flush
  }
}

impl Close for UnixStream {
  move def close {
    @socket.close
  }
}

# A Unix socket server that can accept incoming connections.
class UnixListener {
  # The raw `Socket` wrapped by this `UnixListener`.
  @socket: Socket

  # Creates a new `UnixListener` bound to and listening on the given address.
  #
  # The `backlog` argument can be used to set the listen backlog.
  #
  # # Examples
  #
  # Creating a `UnixListener`:
  #
  #     import std::net::unix::UnixListener
  #
  #     try! UnixListener.new('/tmp/test.sock')
  static def new(address: ref ToString) !! Error -> Self {
    let socket = try Socket.new(STREAM)

    try socket.bind(address)
    try socket.listen

    Self { @socket = socket }
  }

  # Accepts a new incoming connection from `self`.
  #
  # This method does not return until a connection is available.
  #
  # # Examples
  #
  # Accepting a new connection:
  #
  #     import std::net::unix::(UnixListener, UnixStream)
  #
  #     let listener = try! UnixListener.new('/tmp/test.sock')
  #     let client = try! UnixStream.new('/tmp/test.sock')
  #
  #     client.write_string('ping')
  #
  #     let connection = try! listener.accept
  #
  #     try! connection.read_string(4) # => 'ping'
  def accept !! Error -> UnixStream {
    UnixStream { @socket = try @socket.accept }
  }

  # Returns the local address of this socket.
  #
  # See the documentation of `Socket.local_address` for more information.
  def local_address !! Error -> SocketAddress {
    try @socket.local_address
  }

  # Returns the underlying `Socket` object.
  #
  # This method can be used to set additional low-level socket options, without
  # `UnixListener` having to re-define all these methods.
  def socket -> ref Socket {
    @socket
  }
}

impl Close for UnixListener {
  move def close {
    @socket.close
  }
}
