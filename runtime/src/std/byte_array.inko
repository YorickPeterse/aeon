# Arrays of bytes
#
# Byte arrays are arrays specialised for storing individual bytes in the most
# efficient way possible. Unlike a regular `Array` of `Int` values, each
# value only requires a single byte of space, instead of requiring 8 bytes of
# space.
#
# Byte arrays are primarily meant for reading and writing data from/to a
# stream, such as a file or a socket. If you simply want to store a list of
# numbers, you're better off using the `Array` object.
import std::clone::Clone
import std::conversion::ToArray
import std::conversion::ToString
import std::hash::Hash
import std::index::(Index, SetIndex)
import std::length::Length
import std::loop::(repeat)
import std::operators::Equal
import std::option::Option

extern def byte_array_clear(bytes: ref ByteArray)
extern def byte_array_to_string(bytes: ref ByteArray) -> String
extern def byte_array_drain_to_string(bytes: ref ByteArray) -> String

# A type that can be converted to a `ByteArray`.
trait ToByteArray {
  # Converts `self` to a `ByteArray`.
  def to_byte_array -> ByteArray
}

impl ByteArray {
  # Creates a new `ByteArray`.
  #
  # This method will panic if any of the `Int` values passed to this method
  # are not in the range `0..256`.
  #
  # # Examples
  #
  # Creating an empty `ByteArray`:
  #
  #     ByteArray.new
  #
  # Creating a `ByteArray` with values:
  #
  #     ByteArray.new(10, 20, 30)
  static def new(*bytes: Int) -> Self {
    _INKOC.byte_array_from_array(bytes)
  }

  # Returns a `ByteArray` filled with the given byte.
  #
  # The `amount` argument specifies how many times the `value` argument must
  # exist in the `ByteArray`.
  #
  # # Examples
  #
  #     ByteArray.filled(amount: 4, value: 0) # => ByteArray.new(0, 0, 0, 0)
  #
  # # Panics
  #
  # This method panics if the amount is a negative number.
  static def filled(amount: Int, value: Int) -> ByteArray {
    if amount.negative? {
      _INKOC.panic('The fill amount must be equal to or greater than zero')
    }

    let bytes = new

    repeat(amount) do (_) { bytes.push(value) }
    bytes
  }

  # Removes all values from this `ByteArray`.
  #
  # # Examples
  #
  # Removing all values:
  #
  #     let bytes = ByteArray.new(10, 20, 30)
  #
  #     bytes.clear
  #     bytes.length # => 0
  def clear {
    byte_array_clear(self)
  }

  # Pushes a value to the back of the `ByteArray`, returning the pushed value.
  #
  # # Examples
  #
  # Pushing a value into a `ByteArray`:
  #
  #     let bytes = ByteArray.new
  #
  #     bytes.push(10) # => 10
  #     bytes.length   # => 1
  def push(value: Int) {
    self[length] = value
  }

  # Removes a value from the back of the `ByteArray`, returning the removed
  # value.
  #
  # If no value was found, a None is returned instead.
  #
  # # Examples
  #
  # Popping an existing value:
  #
  #     let bytes = ByteArray.new(10)
  #
  #     bytes.pop    # => Option.some(10)
  #     bytes.length # => 0
  #
  # Popping a value when the `ByteArray` is empty:
  #
  #     let bytes = ByteArray.new
  #
  #     bytes.pop # => Option.none
  def pop -> ?Int {
    if length > 0 { Option.some(remove_at(length - 1)) } else { Option.none }
  }

  # Removes the value at the given index, returning the removed value.
  #
  # # Examples
  #
  # Removing an existing value:
  #
  #     let bytes = ByteArray.new(10)
  #
  #     bytes.remove_at(0) # => 10
  #     bytes.length       # => 0
  #
  # # Panics
  #
  # This method panics if the index is out of bounds.
  def remove_at(index: Int) -> Int {
    _INKOC.byte_array_remove(self, index)
  }

  # Returns an `Iterator` that iterates over all values in `self`.
  def iter => Int {
    let mut index = 0
    let max = length

    while index < max {
      yield self[index]

      index += 1
    }
  }

  # Returns a new `String` using the bytes in this `ByteArray`, draining it in
  # the process.
  #
  # After this method is finished, `self` is left empty. This allows one to
  # convert a temporary `ByteArray` into a `String`, without requiring the list
  # of bytes to be allocated twice.
  #
  # # Examples
  #
  # Draining a `ByteArray` into a `String`:
  #
  #     let bytes = ByteArray.new(105, 110, 107, 111)
  #
  #     bytes.drain_to_string # => 'inko'
  #     bytes.empty?          # => True
  def drain_to_string -> String {
    byte_array_drain_to_string(self)
  }

  # Slices `self` into a new `ByteArray`.
  #
  # Similar to slicing a `String`, slicing a `ByteArray` allows one to extract
  # a sub-array by providing a start position and the number of _bytes_ to
  # include starting at the start position.
  #
  # # Examples
  #
  # Slicing a `ByteArray`:
  #
  #     let bytes = ByteArray.new(1, 2, 3, 4)
  #     let sliced = bytes.slice(start: 1, length: 2)
  #
  #     sliced[0] # => 2
  #     sliced[1] # => 3
  def slice(start: Int, length: Int) -> ByteArray {
    let new_array = ByteArray.new
    let mut index = if start >= 0 { start } else { start % self.length }
    let mut until = index + length

    if until >= self.length { until = self.length }

    while index < until {
      new_array.push(self[index].clone)
      index += 1
    }

    new_array
  }

  # Returns the byte at the given index, returning None if the index is out of
  # bounds.
  #
  # # Examples
  #
  # Retrieving an existing byte:
  #
  #     let bytes = ByteArray.new(10, 20)
  #
  #     bytes.get(0) # => Option.some(10)
  #
  # Retrieving a non-existing byte:
  #
  #     let bytes = ByteArray.new(10, 20)
  #
  #     bytes.get(5) # => Option.none
  def get(index: ref Int) -> ?ref Int {
    try Option.some(_INKOC.byte_array_at(self, index)) else Option.none
  }
}

impl Index!(ref Int, Int) for ByteArray {
  # Returns the byte at the given index.
  #
  # # Examples
  #
  # Retrieving an existing byte:
  #
  #     let bytes = ByteArray.new(10, 20)
  #
  #     bytes[0] # => 10
  #
  # # Panics
  #
  # This method panics if the index is out of bounds.
  def [](index: ref Int) -> Int {
    try _INKOC.byte_array_at(self, index) else (err) _INKOC.panic(err as String)
  }
}

impl SetIndex!(ref Int, Int) for ByteArray {
  # Stores a byte at the given index, then returns it.
  #
  # # Examples
  #
  # Setting the value of an existing index:
  #
  #     let bytes = ByteArray.new(10, 20)
  #
  #     bytes[0] = 30 # => 30
  #     bytes[0]      # => 30
  #
  # # Panics
  #
  # This method panics if the index is out of bounds.
  def []=(index: ref Int, value: Int) {
    _INKOC.byte_array_set(self, index, value)
  }
}

impl ToString for ByteArray {
  # Returns a new `String` using the bytes in this `ByteArray`.
  #
  # Any invalid UTF-8 sequences will be replaced with `U+FFFD REPLACEMENT
  # CHARACTER`, which looks like this: ï¿½
  #
  # # Examples
  #
  # Converting a `ByteArray` into a `String`:
  #
  #     let bytes = ByteArray.new(105, 110, 107, 111)
  #
  #     bytes.to_string # => 'inko'
  def to_string -> String {
    byte_array_to_string(self)
  }
}

impl ToArray!(Int) for ByteArray {
  # Converts the `ByteArray` to an `Array!(Int)`.
  #
  # It is recommended to not use this method for very large byte arrays, as
  # an `Int` requires 8 times more memory compared to a single byte. This
  # means that a 1 MB `ByteArray` would require roughly 8 MB of memory.
  #
  # # Examples
  #
  # Converting a `ByteArray`:
  #
  #     let bytes = ByteArray.new(105, 110, 107, 111)
  #
  #     bytes.to_array # => Array.new(105, 110, 107, 111)
  def to_array -> Array!(Int) {
    let integers = Array.new

    for byte in self { integers.push(byte.clone) }

    integers
  }
}

impl Equal for ByteArray {
  # Returns `True` if two `ByteArray` objects are equal to each other.
  #
  # Two `ByteArray` objects are considered equal if they have the exact same
  # values in the exact same order.
  #
  # # Examples
  #
  # Comparing two `ByteArray` objects:
  #
  #     ByteArray.new(10) == ByteArray.new(10) # => True
  #     ByteArray.new(10) == ByteArray.new(20) # => False
  def ==(other: ByteArray) -> Boolean {
    _INKOC.byte_array_equals(self, other)
  }
}

impl Length for ByteArray {
  # Returns the number of bytes in this `ByteArray`.
  #
  # # Examples
  #
  # Obtaining the length of a `ByteArray`
  #
  #     ByteArray.new.length     # => 0
  #     ByteArray.new(10).length # => 1
  def length -> Int {
    _INKOC.byte_array_length(self)
  }
}

impl Clone for ByteArray {
  def clone -> Self {
    let copy = ByteArray.new

    for value in self { copy.push(value) }

    copy
  }
}

impl ToByteArray for ByteArray {
  def to_byte_array -> ByteArray {
    clone
  }
}

impl Hash for ByteArray {}
