import std::ffi::(
  self, Function, Layout, LayoutBuilder, Library, Member, Pointer, Struct, Type
)
import std::process
import std::test::*

def libc_paths -> Array!(String) {
  Array.new(
    # Linux, sometimes a GNU ld script
    'libc.so',

    # Linux, glibc 2.x
    'libc.so.6',

    # CI uses Ubuntu, which puts libc here
    '/lib/x86_64-linux-gnu/libc.so.6',

    # Mac OS
    'libSystem.dylib',

    # Windows
    'msvcrt.dll'
  )
}

# The C `tm` structure, see `man ctime` for more information about this
# structure.
def tm_struct -> Layout {
  ffi.struct do (struct) {
    struct['tm_sec'] = ffi.i32
    struct['tm_min'] = ffi.i32

    struct['tm_hour'] = ffi.i32
    struct['tm_mday'] = ffi.i32

    struct['tm_mon'] = ffi.i32
    struct['tm_year'] = ffi.i32

    struct['tm_wday'] = ffi.i32
    struct['tm_yday'] = ffi.i32

    struct['tm_isdst'] = ffi.i32

    struct['tm_gmtoff'] = ffi.i64
    struct['tm_zone'] = ffi.string
  }
}

def allocate!(R)(size: Int, block: move do (Pointer) -> R) -> R {
  let libc = try! Library.new(libc_paths)
  let calloc = try! libc.function(
    name: 'calloc',
    arguments: Array.new(ffi.size_t, ffi.size_t),
    returns: ffi.pointer
  )

  let free = try! libc.function(
    name: 'free',
    arguments: Array.new(ffi.pointer),
    returns: ffi.void
  )

  let pointer = calloc.call(1, size) as Pointer

  if pointer.null? {
    process.panic(`calloc() failed to allocate {size} bytes of memory`)
  }

  # We defer _after_ the above check, otherwise we may try to free() a NULL
  # pointer in the event of an allocation failure.
  process.defer { free.call(pointer) }
  block.call(pointer)
}

def tests(t: ref Tests) {
  t.group('std::ffi::Pointer.null') do (g) {
    g.test('Creating a NULL pointer') {
      try assert_true(Pointer.null.null?)
    }
  }

  t.group('std::ffi::Pointer.null?') do (g) {
    g.test('Checking if a Pointer is a NULL pointer') {
      try assert_true(Pointer.new(0x0).null?)
      try assert_false(Pointer.new(0x1).null?)
    }
  }

  t.group('std::ffi::Pointer.address') do (g) {
    g.test('Obtaining the address of a Pointer') {
      try assert_equal(Pointer.new(0x4).address, 0x4)
    }
  }

  t.group('std::ffi::Pointer.read') do (g) {
    g.test('Reading a Pointer from a Pointer') {
      allocate(ffi.pointer.size) do (pointer) {
        let value = pointer.read(ffi.pointer, offset: 0) as Pointer

        try assert_true(value.null?)
      }
    }

    g.test('Reading a Float from a Pointer') {
      allocate(ffi.f64.size) do (pointer) {
        try assert_equal(pointer.read(ffi.f64, offset: 0) as Float, 0.0)
      }
    }

    g.test('Reading an Int from a Pointer') {
      allocate(ffi.i32.size) do (pointer) {
        try assert_equal(pointer.read(ffi.i32, offset: 0) as Int, 0)
      }
    }
  }

  t.group('std::ffi::Pointer.write') do (g) {
    g.test('Writing the address of a Pointer to a Pointer') {
      allocate(ffi.pointer.size) do (pointer) {
        allocate(ffi.i32.size) do (input) {
          let input_addr = input.address

          pointer.write(ffi.pointer, input, offset: 0)

          let output = pointer.read(ffi.pointer, offset: 0) as Pointer

          try assert_equal(output.address, input_addr)
        }
      }
    }

    g.test('Writing a String to a Pointer') {
      allocate(5) do (pointer) {
        pointer.write(ffi.string, 'inko', offset: 0)

        try assert_equal(pointer.read(ffi.string, offset: 0) as String, 'inko')
      }
    }

    g.test('Writing a ByteArray to a Pointer') {
      allocate(5) do (pointer) {
        let bytes = 'inko'.to_byte_array

        pointer.write(ffi.byte_array, bytes, offset: 0)

        let output = pointer.read(ffi.byte_array, offset: 0) as ByteArray

        try assert_equal(output, 'inko'.to_byte_array)
      }
    }

  g.test('Writing a float to a Pointer') {
      let type = ffi.f32

      allocate(type.size) do (pointer) {
        pointer.write(type, 1.5, offset: 0)

        try assert_equal(pointer.read(type, offset: 0) as Float, 1.5)
      }
    }

    g.test('Writing a double to a Pointer') {
      let type = ffi.f64

      allocate(type.size) do (pointer) {
        pointer.write(type, 1.5, offset: 0)

        try assert_equal(pointer.read(type, offset: 0) as Float, 1.5)
      }
    }

    g.test('Writing a char to a Pointer') {
      let type = ffi.i8

      allocate(type.size) do (pointer) {
        pointer.write(type, 42, offset: 0)

        try assert_equal(pointer.read(type, offset: 0) as Int, 42)
      }
    }

    g.test('Writing a short to a Pointer') {
      let type = ffi.i16

      allocate(type.size) do (pointer) {
        pointer.write(type, 42, offset: 0)

        try assert_equal(pointer.read(type, offset: 0) as Int, 42)
      }
    }

    g.test('Writing an int to a Pointer') {
      let type = ffi.i32

      allocate(type.size) do (pointer) {
        pointer.write(type, 42, offset: 0)

        try assert_equal(pointer.read(type, offset: 0) as Int, 42)
      }
    }

    g.test('Writing a long to a Pointer') {
      let type = ffi.i64

      allocate(type.size) do (pointer) {
        pointer.write(type, 42, offset: 0)

        try assert_equal(pointer.read(type, offset: 0) as Int, 42)
      }
    }

    g.test('Writing an unsigned char to a Pointer') {
      let type = ffi.u8

      allocate(type.size) do (pointer) {
        pointer.write(type, 42, offset: 0)

        try assert_equal(pointer.read(type, offset: 0) as Int, 42)
      }
    }

    g.test('Writing an unsigned short to a Pointer') {
      let type = ffi.i16

      allocate(type.size) do (pointer) {
        pointer.write(type, 42, offset: 0)

        try assert_equal(pointer.read(type, offset: 0) as Int, 42)
      }
    }

    g.test('Writing an unsigned int to a Pointer') {
      let type = ffi.u32

      allocate(type.size) do (pointer) {
        pointer.write(type, 42, offset: 0)

        try assert_equal(pointer.read(type, offset: 0) as Int, 42)
      }
    }

    g.test('Writing an unsigned long to a Pointer') {
      let type = ffi.u64

      allocate(type.size) do (pointer) {
        pointer.write(type, 42, offset: 0)

        try assert_equal(pointer.read(type, offset: 0) as Int, 42)
      }
    }

    g.test('Writing a size_t to a Pointer') {
      let type = ffi.size_t

      allocate(type.size) do (pointer) {
        pointer.write(type, 42, offset: 0)

        try assert_equal(pointer.read(type, offset: 0) as Int, 42)
      }
    }
  }

  t.group('std::ffi::Pointer.==') do (g) {
    g.test('Comparing two Pointers for equality') {
      try assert_equal(Pointer.null, Pointer.null)
      try assert_not_equal(Pointer.null, Pointer.new(0x1))
    }
  }

  t.group('std::ffi::Library.new') do (g) {
    g.test('Opening a non existing library') {
      try assert_throw {
        try Library.new(Array.new('does-not-exist.so'))
      }
    }

    g.test('Opening a non existing library, with an existing as a fallback') {
      let paths = Array.new('does-not-exist.so')

      paths.append(libc_paths)

      try assert_no_throw { try Library.new(paths) }
    }

    g.test('Opening an existing library') {
      try assert_no_throw { try Library.new(libc_paths) }
    }
  }

  t.group('std::ffi::Library.variable') do (g) {
    g.test('Obtaining a Pointer to an existing variable') {
      let libc = try! Library.new(libc_paths)

      try assert_no_throw { try libc.variable('malloc') }
    }

    g.test('Obtaining a Pointer using a non existing variable') {
      let libc = try! Library.new(libc_paths)

      try assert_throw { try libc.variable('kittens') }
    }
  }

  t.group('std::ffi::Library.close') do (g) {
    g.test('Closing a Library') {
      let lib = try! Library.new(libc_paths)

      try assert_no_throw { lib.close }
    }
  }

  t.group('std::ffi::Library.function') do (g) {
    g.test('Obtaining a Function using an existing function name') {
      let libc = try! Library.new(libc_paths)

      try assert_no_throw {
        try libc.function(
          name: 'malloc',
          arguments: Array.new(ffi.size_t),
          returns: ffi.pointer
        )
      }
    }

    g.test('Obtaining a Function using a non existing function name') {
      let libc = try! Library.new(libc_paths)

      try assert_throw {
        try libc.function(
          name: 'kittens',
          arguments: Array.new,
          returns: ffi.pointer
        )
      }
    }
  }

  t.group('std::ffi::Function.new') do (g) {
    g.test('Obtaining a Function using an existing function name') {
      let libc = try! Library.new(libc_paths)

      try assert_no_throw {
        try Function.new(
          library: libc,
          name: 'malloc',
          arguments: Array.new(ffi.size_t),
          returns: ffi.pointer
        )
      }
    }

    g.test('Obtaining a Function using a non existing function name') {
      let libc = try! Library.new(libc_paths)

      try assert_throw {
        try Function.new(
          library: libc,
          name: 'kittens',
          arguments: Array.new,
          returns: ffi.pointer
        )
      }
    }
  }

  t.group('std::ffi::Function.call') do (g) {
    g.test('Calling a function with a valid number of arguments') {
      let libc = try! Library.new(libc_paths)
      let time_func = try! libc.function('time', Array.new(ffi.pointer), ffi.i64)
      let timestamp = time_func.call(Pointer.null) as Int

      try assert_true(timestamp.positive?)
    }

    g.test('Calling a Function with an invalid number of arguments') {
      try assert_panic(
        "import std::ffi::(self, Library)

        let libc = try! Library.new(
          Array.new(
            'libc.so',
            'libc.so.6',
            '/lib/x86_64-linux-gnu/libc.so.6',
            'libSystem.dylib',
            'msvcrt.dll'
          )
        )

        let time = try! libc.function('time', Array.new(ffi.pointer), ffi.i64)

        time.call"
      )
    }
  }

  t.group('std::ffi::Member.name') do (g) {
    g.test('Obtaining the name of a Member') {
      let member = Member.new(name: 'tm_sec', type: ffi.i32, offset: 0)

      try assert_equal(member.name, 'tm_sec')
    }
  }

  t.group('std::ffi::Member.type') do (g) {
    g.test('Obtaining the type of a Member') {
      let member = Member.new(name: 'tm_sec', type: ffi.i32, offset: 0)

      try assert_equal(member.type, ffi.i32)
    }
  }

  t.group('std::ffi::Member.offset') do (g) {
    g.test('Obtaining the offset of a Member') {
      let member = Member.new(name: 'tm_sec', type: ffi.i32, offset: 4)

      try assert_equal(member.offset, 4)
    }
  }

  t.group('std::ffi::Layout.alignment') do (g) {
    g.test('Obtaining the alignment of a Layout') {
      let layout = Layout.new(members: Map.new, alignment: 8, size: 4)

      try assert_equal(layout.alignment, 8)
    }
  }

  t.group('std::ffi::Layout.size') do (g) {
    g.test('Obtaining the size of a Layout') {
      let layout = Layout.new(members: Map.new, alignment: 8, size: 4)

      try assert_equal(layout.size, 4)
    }
  }

  t.group('std::ffi::Layout.[]') do (g) {
    g.test('Obtaining a Member using a valid name') {
      let member = Member.new(name: 'tm_sec', type: ffi.i32, offset: 0)
      let members = Map.new

      members['tm_sec'] = member

      let layout = Layout.new(members: members, alignment: 8, size: 4)

      try assert_equal(layout['tm_sec'].name, 'tm_sec')
      try assert_equal(layout['tm_sec'].type, ffi.i32)
    }

    g.test('Obtaining a Member using an invalid name') {
      try assert_panic(
        "import std::ffi::Layout

        let layout = Layout.new(members: Map.new, alignment: 8, size: 4)

        layout['tm_sec']"
      )
    }
  }

  t.group('std::ffi::Layout.from_pointer') do (g) {
    g.test('Creating a Struct from a Layout using a Pointer') {
      let tm = tm_struct

      allocate(tm.size) do (pointer) {
        let size = tm.size
        let align = tm.alignment
        let struct = tm.from_pointer(pointer)

        try assert_equal(struct.size, size)
        try assert_equal(struct.alignment, align)
      }
    }
  }

  t.group('std::ffi::Struct.size') do (g) {
    g.test('Obtaining the size of a Struct') {
      let layout = Layout.new(members: Map.new, alignment: 8, size: 4)
      let struct = Struct.new(pointer: Pointer.null, layout: layout)

      try assert_equal(struct.size, 4)
    }
  }

  t.group('std::ffi::Struct.alignment') do (g) {
    g.test('Obtaining the alignment of a Struct') {
      let layout = Layout.new(members: Map.new, alignment: 8, size: 4)
      let struct = Struct.new(pointer: Pointer.null, layout: layout)

      try assert_equal(struct.alignment, 8)
    }
  }

  t.group('std::ffi::Struct.pointer') do (g) {
    g.test('Obtaining the alignment of a Struct') {
      let layout = Layout.new(members: Map.new, alignment: 8, size: 4)
      let struct = Struct.new(pointer: Pointer.null, layout: layout)

      try assert_equal(struct.pointer, Pointer.null)
    }
  }

  t.group('std::ffi::Struct.[]') do (g) {
    g.test('Reading the value of a valid Struct member') {
      let tm = tm_struct

      allocate(tm.size) do (pointer) {
        let struct = Struct.new(pointer: pointer, layout: tm)

        try assert_equal(struct['tm_sec'] as Int, 0)
      }
    }

    g.test('Reading the value of an invalid Struct member') {
      try assert_panic(
        "import std::ffi::(Layout, Struct, Pointer)

        let layout = Layout.new(members: Map.new, alignment: 8, size: 4)
        let struct = Struct.new(pointer: Pointer.null, layout: layout)

        struct['kittens']"
      )
    }
  }

  t.group('std::ffi::Struct.[]=') do (g) {
    g.test('Writing a value to a valid Struct member') {
      let tm = tm_struct

      allocate(tm.size) do (pointer) {
        let struct = Struct.new(pointer: pointer, layout: tm)

        struct['tm_sec'] = 42

        try assert_equal(struct['tm_sec'] as Int, 42)
      }
    }

    g.test('Writing a value to an invalid Struct member') {
      try assert_panic(
        "import std::ffi::(Layout, Struct, Pointer)

        let layout = Layout.new(members: Map.new, alignment: 8, size: 4)
        let struct = Struct.new(pointer: Pointer.null, layout: layout)

        struct['kittens'] = 42"
      )
    }
  }

  t.group('std::ffi::LayoutBuilder.[]=') do (g) {
    g.test('Defining a new struct member') {
      let builder = LayoutBuilder.new

      builder['tm_sec'] = ffi.i32

      let layout = builder.to_layout

      try assert_equal(layout['tm_sec'].type, ffi.i32)
    }

    g.test('Defining an existing struct member') {
      try assert_panic(
        "import std::ffi::(self, LayoutBuilder)

        let builder = LayoutBuilder.new

        builder['tm_sec'] = ffi.i32
        builder['tm_sec'] = ffi.i32"
      )
    }
  }

  t.group('std::ffi::LayoutBuilder.to_layout') do (g) {
    g.test('Creating a Layout with padding') {
      let builder = LayoutBuilder.new

      builder['foo'] = ffi.i32
      builder['bar'] = ffi.i64

      let layout = builder.to_layout

      try assert_equal(layout.alignment, 8)
      try assert_equal(layout.size, 16)
      try assert_equal(layout['foo'].offset, 0)
      try assert_equal(layout['bar'].offset, 8)
    }

    g.test('Creating a Layout without padding') {
      let builder = LayoutBuilder.new

      builder.disable_padding

      builder['foo'] = ffi.i32
      builder['bar'] = ffi.i64

      let layout = builder.to_layout

      try assert_equal(layout.alignment, 1)
      try assert_equal(layout.size, 12)
      try assert_equal(layout['foo'].offset, 0)
      try assert_equal(layout['bar'].offset, 4)
    }
  }

  t.group('std::ffi.struct') do (g) {
    g.test('Creating a Layout') {
      let layout = ffi.struct do (struct) {
        struct['foo'] = ffi.i32
        struct['bar'] = ffi.i64
      }

      try assert_equal(layout.alignment, 8)
      try assert_equal(layout.size, 16)
      try assert_equal(layout['foo'].offset, 0)
      try assert_equal(layout['bar'].offset, 8)
    }
  }

  t.group('std::ffi::Type.to_int') do (g) {
    g.test('Obtaining the type ID of a Type') {
      let type = Type.new(2)

      try assert_equal(type.to_int, 2)
    }
  }

  t.group('std::ffi::Type.size') do (g) {
    g.test('Obtaining the size of a Type') {
      let type = Type.new(2)

      try assert_equal(type.size, 8)
    }
  }

  t.group('std::ffi::Type.alignment') do (g) {
    g.test('Obtaining the alignment of a Type') {
      let type = Type.new(2)

      try assert_equal(type.alignment, 8)
    }
  }

  t.group('std::ffi.void') do (g) {
    g.test('Creating a new void type') {
      try assert_equal(ffi.void.to_int, 0)
    }
  }

  t.group('std::ffi.pointer') do (g) {
    g.test('Creating a new pointer type') {
      try assert_equal(ffi.pointer.to_int, 1)
    }
  }

  t.group('std::ffi.f64') do (g) {
    g.test('Creating a new double type') {
      try assert_equal(ffi.f64.to_int, 2)
    }
  }

  t.group('std::ffi.f32') do (g) {
    g.test('Creating a new float type') {
      try assert_equal(ffi.f32.to_int, 3)
    }
  }

  t.group('std::ffi.i8') do (g) {
    g.test('Creating a new char type') {
      try assert_equal(ffi.i8.to_int, 4)
    }
  }

  t.group('std::ffi.i16') do (g) {
    g.test('Creating a new short type') {
      try assert_equal(ffi.i16.to_int, 5)
    }
  }

  t.group('std::ffi.i32') do (g) {
    g.test('Creating a new int type') {
      try assert_equal(ffi.i32.to_int, 6)
    }
  }

  t.group('std::ffi.i64') do (g) {
    g.test('Creating a new long type') {
      try assert_equal(ffi.i64.to_int, 7)
    }
  }

  t.group('std::ffi.u8') do (g) {
    g.test('Creating a new unsigned char type') {
      try assert_equal(ffi.u8.to_int, 8)
    }
  }

  t.group('std::ffi.u16') do (g) {
    g.test('Creating a new unsigned short type') {
      try assert_equal(ffi.u16.to_int, 9)
    }
  }

  t.group('std::ffi.u32') do (g) {
    g.test('Creating a new unsigned int type') {
      try assert_equal(ffi.u32.to_int, 10)
    }
  }

  t.group('std::ffi.u64') do (g) {
    g.test('Creating a new unsigned long type') {
      try assert_equal(ffi.u64.to_int, 11)
    }
  }

  t.group('std::ffi.size_t') do (g) {
    g.test('Creating a new size_t type') {
      try assert_equal(ffi.size_t.to_int, 14)
    }
  }

  t.group('std::ffi.string') do (g) {
    g.test('Creating a new string type') {
      try assert_equal(ffi.string.to_int, 12)
    }
  }

  t.group('std::ffi.byte_array') do (g) {
    g.test('Creating a new byte array type') {
      try assert_equal(ffi.byte_array.to_int, 13)
    }
  }
}
