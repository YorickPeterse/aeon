import std::fs::file
import std::fs::path::Path
import std::net::bits::MAXIMUM_LISTEN_BACKLOG
import std::net::unix::(
  DGRAM, RAW, SEQPACKET, STREAM, Socket, SocketAddress, UnixDatagram,
  UnixListener, UnixStream
)
import std::os
import std::process
import std::test::*
import test::fixtures

def with_path(block: do (Path)) {
  let path = fixtures.temporary_file_path

  process.defer {
    try file.remove(path) else Nil
  }

  block.call(path)
}

def with_paths(block: do (Path, Path)) {
  let path1 = fixtures.temporary_file_path
  let path2 = (path1.to_string + '2').into_path

  process.defer {
    try file.remove(path1) else Nil
    try file.remove(path2) else Nil
  }

  block.call(path1, path2)
}

def abstract_socket_address -> String {
  "\0inko-tests-" + process.current.identifier.to_string
}

def tests(t: ref Tests) {
  t.group('std::net::unix::SocketAddress.to_path') do (g) {
    g.test('Converting a path based SocketAddress to a Path') {
      let addr = SocketAddress.new('foo.sock')
      let path = addr.to_path

      try assert_true(path.some?)
      try assert_equal(addr.to_path.get, 'foo.sock'.to_path)
    }

    g.test('Converting an abstract SocketAddress to a Path') {
      let addr = SocketAddress.new("\0foo")

      try assert_true(addr.to_path.none?)
    }

    g.test('Converting an unnamed SocketAddress to a Path') {
      let addr = SocketAddress.new

      try assert_true(addr.to_path.none?)
    }
  }

  t.group('std::net::unix::SocketAddress.to_string') do (g) {
    g.test('Converting a path based SocketAddress to a String') {
      let addr = SocketAddress.new('foo.sock')

      try assert_equal(addr.to_string, 'foo.sock')
    }

    g.test('Converting an abstract SocketAddress to a String') {
      let addr = SocketAddress.new("\0foo")

      try assert_equal(addr.to_string, "\0foo")
    }

    g.test('Converting an unnamed SocketAddress to a String') {
      let addr = SocketAddress.new

      try assert_equal(addr.to_string, '')
    }
  }

  t.group('std::net::unix::SocketAddress.abstract?') do (g) {
    g.test('Checking if a SocketAddress is an abstract address') {
      try assert_false(SocketAddress.new.abstract?)
      try assert_false(SocketAddress.new('foo.sock').abstract?)
      try assert_true(SocketAddress.new("\0foo").abstract?)
    }
  }

  t.group('std::net::unix::SocketAddress.unnamed?') do (g) {
    g.test('Checking if a SocketAddress is an unnamed address') {
      try assert_false(SocketAddress.new('foo.sock').unnamed?)
      try assert_false(SocketAddress.new("\0foo").unnamed?)
      try assert_true(SocketAddress.new.unnamed?)
    }
  }

  t.group('std::net::unix::SocketAddress.==') do (g) {
    g.test('Comparing two SocketAddress objects') {
      try assert_equal(SocketAddress.new('a.sock'), SocketAddress.new('a.sock'))
      try assert_not_equal(SocketAddress.new('a.sock'), SocketAddress.new('b.sock'))
    }
  }

  if os.unix? {
    t.group('std::net::unix::Socket.new') do (g) {
      g.test('Creating datagram Unix socket') {
        try assert_no_throw { try Socket.new(DGRAM) }
      }

      g.test('Creating a stream Unix socket') {
        try assert_no_throw { try Socket.new(STREAM) }
      }

      if not os.mac? {
        g.test('Creating a sequential packet Unix socket') {
          try assert_no_throw { try Socket.new(SEQPACKET) }
        }

        g.test('Creating a raw Unix socket') {
          try assert_no_throw { try Socket.new(RAW) }
        }
      }

      g.test('Creating a Unix socket of an invalid kind') {
        try assert_panic(
          'import std::net::unix::Socket

          try! Socket.new(9999)'
        )
      }
    }

    t.group('std::net::unix::Socket.bind') do (g) {
      g.test('Binding a Unix socket to a path') {
        let socket = try! Socket.new(STREAM)

        with_path do (path) {
          try assert_no_throw { try socket.bind(path) }
        }
      }

      g.test('Binding a Unix socket to a path that already exists') {
        let socket1 = try! Socket.new(STREAM)
        let socket2 = try! Socket.new(STREAM)

        with_path do (path) {
          try! socket1.bind(path.clone)

          try assert_throw { try socket2.bind(path) }
        }
      }

      if os.linux? {
        g.test('Binding a Unix socket to an abstract address') {
          let socket = try! Socket.new(STREAM)

          try assert_no_throw { try socket.bind(abstract_socket_address) }
        }
      }
    }

    t.group('std::net::unix::Socket.connect') do (g) {
      g.test('Connecting to an invalid address') {
        let socket = try! Socket.new(STREAM)

        with_path do (path) {
          try assert_throw { try socket.connect(path) }
        }
      }

      g.test('Connecting to a valid address') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)
          let stream = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen

          try assert_no_throw { try stream.connect(path) }
        }
      }

      if os.linux? {
        g.test('Connecting to an abstract address') {
          let listener = try! Socket.new(STREAM)
          let stream = try! Socket.new(STREAM)

          try! listener.bind(abstract_socket_address)
          try! listener.listen

          try assert_no_throw {
            try stream.connect(abstract_socket_address)
          }
        }
      }
    }

    t.group('std::net::unix::Socket.listen') do (g) {
      g.test('Marking a Socket as listening') {
        with_path do (path) {
          let socket = try! Socket.new(STREAM)

          try! socket.bind(path)

          try assert_equal(try! socket.listen, MAXIMUM_LISTEN_BACKLOG)
        }
      }
    }

    t.group('std::net::unix::Socket.accept') do (g) {
      g.test('Accepting an incoming connection') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)
          let stream = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen
          try! stream.connect(path)

          let client = try! listener.accept

          try assert_equal(try! client.peer_address, try! stream.local_address)
        }
      }
    }

    t.group('std::net::unix::Socket.send_string_to') do (g) {
      g.test('Sending a String to a specific address') {
        with_path do (path) {
          let socket = try! Socket.new(DGRAM)

          try! socket.bind(path.clone)
          try! socket.send_string_to('ping', path)

          try assert_equal(try! socket.read_string(size: 4), 'ping')
        }
      }
    }

    t.group('std::net::unix::Socket.send_bytes_to') do (g) {
      g.test('Sending a ByteArray to a specific address') {
        with_path do (path) {
          let socket = try! Socket.new(DGRAM)
          let bytes = 'ping'.to_byte_array

          try! socket.bind(path.clone)
          try! socket.send_bytes_to(bytes, path)

          try assert_equal(try! socket.read_string(size: 4), 'ping')
        }
      }
    }

    t.group('std::net::unix::Socket.receive_from') do (g) {
      g.test('Receiving a message and a SocketAddress') {
        with_paths do (path1, path2) {
          let listener = try! Socket.new(DGRAM)
          let client = try! Socket.new(DGRAM)

          try! listener.bind(path1.clone)
          try! client.bind(path2)
          try! client.send_string_to('ping', path1)

          let bytes = ByteArray.new
          let sender = try! listener.receive_from(bytes: bytes, size: 4)

          try assert_equal(sender, try! client.local_address)
          try assert_equal(bytes.to_string, 'ping')
        }
      }
    }

    t.group('std::net::unix::Socket.local_address') do (g) {
      g.test('Obtaining the local address of an unbound socket') {
        let socket = try! Socket.new(DGRAM)
        let address = try! socket.local_address

        try assert_equal(address, SocketAddress.new)
      }

      g.test('Obtaining the local address of a bound socket') {
        with_path do (path) {
          let socket = try! Socket.new(DGRAM)

          try! socket.bind(path.clone)

          let local_address = try! socket.local_address

          try assert_equal(local_address.to_string, path.to_string)
        }
      }
    }

    t.group('std::net::unix::Socket.peer_address') do (g) {
      g.test('Obtaining the peer address of a disconnected socket') {
        let socket = try! Socket.new(DGRAM)

        try assert_throw { try socket.peer_address }
      }

      g.test('Obtaining the peer address of a connected socket') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)
          let client = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen
          try! client.connect(path)

          try assert_equal(try! client.peer_address, try! listener.local_address)
        }
      }
    }

    t.group('std::net::unix::Socket.read_bytes') do (g) {
      g.test('Reading bytes from a Socket') {
        with_path do (path) {
          let socket = try! Socket.new(DGRAM)
          let bytes = ByteArray.new

          try! socket.bind(path.clone)
          try! socket.send_string_to('ping', path)

          let read = try! socket.read_bytes(bytes: bytes, size: 4)

          try assert_equal(bytes.to_string, 'ping')
          try assert_equal(read, 4)
        }
      }
    }

    t.group('std::net::unix::Socket.write_bytes') do (g) {
      g.test('Writing bytes to a Socket') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)
          let stream = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen
          try! stream.connect(path)

          let written = try! stream.write_bytes('ping'.to_byte_array)
          let connection = try! listener.accept
          let message = try! connection.read_string(size: 4)

          try assert_equal(message, 'ping')
          try assert_equal(written, 4)
        }
      }
    }

    t.group('std::net::unix::Socket.close') do (g) {
      g.test('Closing a Socket') {
        let socket = try! Socket.new(STREAM)

        socket.close
      }
    }

    t.group('std::net::unix::Socket.flush') do (g) {
      g.test('Flushing a Socket') {
        let socket = try! Socket.new(STREAM)

        try assert_equal(socket.flush, Nil)
      }
    }

    t.group('std::net::unix::Socket.receive_buffer_size') do (g) {
      g.test('Setting and obtaining the value of the SO_RCVBUF option') {
        let socket = try! Socket.new(STREAM)

        try! socket.receive_buffer_size = 256

        try assert_true(try! { socket.receive_buffer_size } >= 256)
      }
    }

    t.group('std::net::unix::Socket.send_buffer_size') do (g) {
      g.test('Setting and obtaining the value of the SO_SNDBUT option') {
        let socket = try! Socket.new(STREAM)

        try! socket.send_buffer_size = 256

        try assert_true(try! { socket.send_buffer_size } >= 256)
      }
    }

    t.group('std::net::unix::Socket.shutdown_read') do (g) {
      g.test('Shutting down the reading half of a Socket') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)
          let stream = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen
          try! stream.connect(path)
          try! stream.shutdown_read

          let message = try! stream.read_string(4)

          try assert_equal(message, '')
        }
      }
    }

    t.group('std::net::socket::Socket.shutdown_write') do (g) {
      g.test('Shutting down the writing half of a Socket') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)
          let stream = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen
          try! stream.connect(path)
          try! stream.shutdown_write

          try assert_throw { try stream.write_string('ping') }
        }
      }
    }

    t.group('std::net::socket::Socket.shutdown') do (g) {
      g.test('Shutting down the writing half of a Socket') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)
          let stream = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen
          try! stream.connect(path)
          try! stream.shutdown

          try assert_throw { try stream.write_string('ping') }
        }
      }

      g.test('Shutting down the reading half of a Socket') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)
          let stream = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen
          try! stream.connect(path)
          try! stream.shutdown

          let message = try! stream.read_string(4)

          try assert_equal(message, '')
        }
      }
    }

    t.group('std::net::unix::UnixDatagram') do (g) {
      g.test('Creating a UnixDatagram') {
        with_path do (path) {
          try assert_no_throw { try UnixDatagram.new(path) }
        }
      }

      g.test('Creating a UnixDatagram using an address that is already in use') {
        with_path do (path) {
          try! UnixDatagram.new(path.clone)

          try assert_throw { try UnixDatagram.new(path) }
        }
      }
    }

    t.group('std::net::unix::UnixDatagram.connect') do (g) {
      g.test('Connecting a UnixDatagram') {
        with_paths do (path1, path2) {
          let socket1 = try! UnixDatagram.new(path1)
          let socket2 = try! UnixDatagram.new(path2.clone)

          process.defer {
            socket1.close
            socket2.close
          }

          try assert_no_throw { try socket1.connect(path2) }
        }
      }

      g.test('Reading and writing from a connected UdpSocket') {
        with_paths do (path1, path2) {
          let socket1 = try! UnixDatagram.new(path1)
          let socket2 = try! UnixDatagram.new(path2.clone)

          try! socket1.connect(path2)
          try! socket1.write_string('ping')

          try assert_equal(try! socket2.read_string(4), 'ping')
        }
      }
    }

    t.group('std::net::unix::UnixDatagram.send_to') do (g) {
      g.test('Sending a message to a specific address') {
        with_path do (path) {
          let socket = try! UnixDatagram.new(path.clone)

          try! socket.send_string_to('ping', path)

          try assert_equal(try! socket.read_string(size: 4), 'ping')
        }
      }
    }

    t.group('std::net::unix::UnixDatagram.receive_from') do (g) {
      g.test('Receiving a message and a SocketAddress') {
        with_paths do (path1, path2) {
          let listener = try! UnixDatagram.new(path1.clone)
          let client = try! UnixDatagram.new(path2)

          try! client.send_string_to('ping', path1)

          let bytes = ByteArray.new
          let sender = try! listener.receive_from(bytes: bytes, size: 4)

          try assert_equal(sender, try! client.local_address)
          try assert_equal(bytes.to_string, 'ping')
        }
      }
    }

    t.group('std::net::unix::UnixDatagram.local_address') do (g) {
      g.test('Obtaining the local address of a UnixDatagram') {
        with_path do (path) {
          let socket = try! UnixDatagram.new(path.clone)
          let local_address = try! socket.local_address

          try assert_equal(local_address.to_string, path.to_string)
        }
      }
    }

    t.group('std::net::unix::UnixDatagram.read_bytes') do (g) {
      g.test('Reading bytes from a UnixDatagram') {
        with_path do (path) {
          let socket = try! UnixDatagram.new(path.clone)
          let bytes = ByteArray.new

          try! socket.send_string_to('ping', path)

          let read = try! socket.read_bytes(bytes: bytes, size: 4)

          try assert_equal(bytes.to_string, 'ping')
          try assert_equal(read, 4)
        }
      }
    }

    t.group('std::net::unix::UnixDatagram.write_bytes') do (g) {
      g.test('Writing bytes to a connected UnixDatagram') {
        with_paths do (path1, path2) {
          let server = try! UnixDatagram.new(path1.clone)
          let client = try! UnixDatagram.new(path2)

          try! client.connect(path1)

          let written = try! client.write_bytes('ping'.to_byte_array)
          let message = try! server.read_string(size: 4)

          try assert_equal(message, 'ping')
          try assert_equal(written, 4)
        }
      }

      g.test('Writing bytes to a disconnected UnixDatagram') {
        with_path do (path) {
          let socket = try! UnixDatagram.new(path)

          try assert_throw {
            try socket.write_bytes('ping'.to_byte_array)
          }
        }
      }
    }

    t.group('std::net::unix::UnixDatagram.close') do (g) {
      g.test('Closing a UnixDatagram') {
        with_path do (path) {
          let socket = try! UnixDatagram.new(path)

          socket.close
        }
      }
    }

    t.group('std::net::unix::UnixDatagram.flush') do (g) {
      g.test('Flushing a UnixDatagram') {
        with_path do (path) {
          let socket = try! UnixDatagram.new(path)

          try assert_equal(socket.flush, Nil)
        }
      }
    }

    t.group('std::net::unix::UnixStream.new') do (g) {
      g.test('Creating a new UnixStream') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen

          try assert_no_throw { try UnixStream.new(path) }
        }
      }
    }

    t.group('std::net::unix::UnixStream.local_address') do (g) {
      g.test('Obtaining the local address') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen

          let stream = try! UnixStream.new(path)
          let local_addr = try! stream.local_address

          try assert_equal(local_addr.to_string, '')
        }
      }
    }

    t.group('std::net::unix::UnixStream.peer_address') do (g) {
      g.test('Obtaining the peer address') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen

          let stream = try! UnixStream.new(path.clone)
          let peer_addr = try! stream.peer_address

          try assert_equal(peer_addr.to_string, path.to_string)
        }
      }
    }

    t.group('std::net::unix::UnixStream.read_bytes') do (g) {
      g.test('Reading bytes from a UnixStream') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen

          let stream = try! UnixStream.new(path)
          let bytes = ByteArray.new

          try! listener.accept.write_string('ping')
          try! stream.read_bytes(bytes: bytes, size: 4)

          try assert_equal(bytes.to_string, 'ping')
        }
      }
    }


    t.group('std::net::unix::UnixStream.write_bytes') do (g) {
      g.test('Writing bytes to a UnixStream') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen

          let stream = try! UnixStream.new(path)
          let connection = try! listener.accept

          try! stream.write_bytes('ping'.to_byte_array)

          try assert_equal(try! connection.read_string(4), 'ping')
        }
      }
    }

    t.group('std::net::unix::UnixStream.write_string') do (g) {
      g.test('Writing a String to a UnixStream') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen

          let stream = try! UnixStream.new(path)
          let connection = try! listener.accept

          try! stream.write_string('ping')

          try assert_equal(try! connection.read_string(4), 'ping')
        }
      }
    }

    t.group('std::net::unix::UnixStream.close') do (g) {
      g.test('Closing a UnixStream') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen

          let stream = try! UnixStream.new(path)

          stream.close
        }
      }
    }

    t.group('std::net::unix::UnixStream.flush') do (g) {
      g.test('Flushing a UnixStream') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen

          let stream = try! UnixStream.new(path)

          try assert_equal(stream.flush, Nil)
        }
      }
    }

    t.group('std::net::unix::UnixStream.shutdown_read') do (g) {
      g.test('Shutting down the reading half of a UnixStream') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen

          let stream = try! UnixStream.new(path)

          try! stream.shutdown_read

          let message = try! stream.read_string(4)

          try assert_equal(message, '')
        }
      }
    }

    t.group('std::net::socket::UnixStream.shutdown_write') do (g) {
      g.test('Shutting down the writing half of a UnixStream') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen

          let stream = try! UnixStream.new(path)

          try! stream.shutdown_write

          try assert_throw { try stream.write_string('ping') }
        }
      }
    }

    t.group('std::net::socket::UnixStream.shutdown') do (g) {
      g.test('Shutting down the writing half of a UnixStream') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen

          let stream = try! UnixStream.new(path)

          try! stream.shutdown

          try assert_throw { try stream.write_string('ping') }
        }
      }

      g.test('Shutting down the reading half of a UnixStream') {
        with_path do (path) {
          let listener = try! Socket.new(STREAM)

          try! listener.bind(path.clone)
          try! listener.listen

          let stream = try! UnixStream.new(path)

          try! stream.shutdown

          let message = try! stream.read_string(4)

          try assert_equal(message, '')
        }
      }
    }

    t.group('std::net::unix::UnixListener.new') do (g) {
      g.test('Creating a UnixListener') {
        with_path do (path) {
          try assert_no_throw { try UnixListener.new(path) }
        }
      }
    }

    t.group('std::net::unix::UnixListener.accept') do (g) {
      g.test('Accepting an incoming connection') {
        with_path do (path) {
          let listener = try! UnixListener.new(path.clone)
          let stream = try! UnixStream.new(path)
          let connection = try! listener.accept

          try assert_equal(
            try! connection.local_address,
            try! stream.peer_address
          )
        }
      }
    }

    t.group('std::net::unix::UnixListener.local_address') do (g) {
      g.test('Obtaining the local address') {
        with_path do (path) {
          let listener = try! UnixListener.new(path.clone)
          let addr = try! listener.local_address

          try assert_equal(addr.to_string, path.to_string)
        }
      }
    }

    t.group('std::net::unix::UnixListener.close') do (g) {
      g.test('Closing a UnixListener') {
        with_path do (path) {
          let listener = try! UnixListener.new(path)

          try assert_equal(listener.close, Nil)
        }
      }
    }
  }
}
