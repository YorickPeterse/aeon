import std::env
import std::fs::dir
import std::fs::path::(Path, SEPARATOR)
import std::process
import std::string_buffer::StringBuffer
import std::test::*
import std::test::test::Failure

# The path of the base temporary directory, such as /tmp on Linux.
let TEMP = env.temporary_directory.to_string

def temp_dir(components: Array!(String)) -> Path {
  let buffer = StringBuffer
    .new(TEMP, SEPARATOR, 'inko-test-' + process.current.identifier.to_string)

  for component in components.into_iter {
    buffer.push(SEPARATOR)
    buffer.push(component)
  }

  buffer.to_string.into_path
}

def with_temp_dir(
  components: Array!(String),
  block: do (Path) !! Failure
) !! Failure {
  let root = temp_dir(Array.new)
  let path = temp_dir(components)

  # Without the root directory being present our tests are useless. As such, if
  # this fails we can do nothing but panic.
  try! dir.create(root)

  process.defer {
    # If we can't remove the directory that's OK, since it resides inside the
    # systems' temporary directory.
    try dir.remove_all(root) else Nil
  }

  try block.call(path)
}

def tests(t: ref Tests) {
  t.group('std::fs::dir.create') do (g) {
    g.test('Creating a valid directory') {
      try with_temp_dir(Array.new('foo')) do (path) {
        try! dir.create(path)

        try assert_true(path.directory?)
      }
    }

    g.test('Creating an already existing directory') {
      try with_temp_dir(Array.new('foo')) do (path) {
        try! dir.create(path)

        try assert_throw { try dir.create(path) }
      }
    }
  }

  t.group('std::fs::dir::create_all') do (g) {
    g.test('Creating a directory recursively') {
      try with_temp_dir(Array.new('foo', 'bar')) do (path) {
        try! dir.create_all(path)

        try assert_true(path.directory?)
      }
    }

    g.test('Creating an existing directory recursively') {
      try with_temp_dir(Array.new('foo', 'bar')) do (path) {
        try! dir.create_all(path)

        try assert_no_throw { try dir.create_all(path) }
      }
    }
  }

  t.group('std::fs::dir.remove') do (g) {
    g.test('Removing an empty directory') {
      try with_temp_dir(Array.new('foo')) do (path) {
        try! dir.create(path)
        try! dir.remove(path)

        try assert_false(path.directory?)
      }
    }

    g.test('Removing a non-empty directory') {
      try with_temp_dir(Array.new('foo', 'bar')) do (path) {
        try! dir.create(path: path, recursive: True)

        try assert_throw { try dir.remove(temp_dir) }
      }
    }
  }

  t.group('std::fs::dir.remove_all') do (g) {
    g.test('Removing an empty directory recursively') {
      try with_temp_dir(Array.new('foo', 'bar')) do (path) {
        try! dir.create_all(path)
        try! dir.remove_all(path)

        try assert_false(path.directory?)
      }
    }

    g.test('Removing a non-empty directory recursively') {
      let root = temp_dir

      try with_temp_dir(Array.new('foo', 'bar')) do (path) {
        try! dir.create_all(path)
        try! dir.remove_all(root)

        try assert_false(root.directory?)
      }
    }
  }

  t.group('std::fs::dir.list') do (g) {
    g.test('Listing the contents of an empty directory') {
      try with_temp_dir(Array.new) do (path) {
        let contents = try! dir.list(path)

        try assert_equal(contents, Array.new)
      }
    }

    g.test('Listing the contents of a non-existing directory') {
      try with_temp_dir(Array.new('foo')) do (path) {
        try assert_throw { try dir.list(path) }
      }
    }

    g.test('Listing the contents of a non-empty directory') {
      let root = temp_dir

      try with_temp_dir(Array.new('foo')) do (path) {
        try! dir.create(path)

        let contents = try! dir.list(root)

        try assert_equal(contents.length, 1)
        try assert_equal(contents[0], path)
      }
    }
  }
}
